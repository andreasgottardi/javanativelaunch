plugins {
    id 'java-library'
    id 'application'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
	
	gcc = MINGW + System.getProperty("file.separator") + 'gcc.exe'
	make = MINGW + System.getProperty("file.separator") + 'mingw32-make.exe'
	windres = MINGW + System.getProperty("file.separator") + 'windres.exe'
	execPath = MINGW + System.getProperty("path.separator") + System.getenv("PATH")
	
	/* Load path to JDK from gradle.properties. */
	javaHome = JAVA_HOME
	
	/* Load path to Nsis from gradle.properties. */
	nsisHome = NSIS_HOME
	
	mainClass = 'compilerex.Library'
	
	progName = 'JavaNativeLaunch'
	progVers = '0.0.1'
	
	junitvers = '5.7.0'
}

mainClassName = mainClass
version = progVers

repositories {
    jcenter()
}

dependencies {

	implementation 'org.slf4j:slf4j-api:1.7.30'
	implementation 'ch.qos.logback:logback-core:1.2.3'
	implementation 'ch.qos.logback:logback-classic:1.2.3'
	
    testImplementation 'org.junit.jupiter:junit-jupiter-api:' + junitvers
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:' + junitvers
}

test {
    useJUnitPlatform()
}

jar {
	manifest {
		attributes 'Main-Class': mainClass
	}
}

/* Exports dependency jar files to the setup/lib directory. */
task exportDependencies(type: Copy, group: 'distribution', description: 'Exports the application dependencies into a export folder.') {

	dependsOn build
	mustRunAfter build
	
	into "${buildDir}/setup/lib"
	from configurations.runtimeClasspath
}

/* Exports the generated jar file into the setup/lib folder. */
tasks.withType(Jar) {
	destinationDirectory = file("${buildDir}/setup/lib")
}

/* Deletes an probably existing "java.c" file in "src/main/c" folder. */
task cleanJavaC(type: Delete, group: 'build', description: 'Deletes previously generated java.c file.') {

	dependsOn exportDependencies
	mustRunAfter exportDependencies
	
	delete 'src/main/c/java.c'
}

/* Generates a java.c file from the template with the main class name inserted. */
task generateJavaC(type: Copy, group: 'build', description: 'Generates a new java.c file with main class set correctly.') {
	
	dependsOn cleanJavaC
	mustRunAfter cleanJavaC
	
	from("src/main/c"){
      include "java.tpl.c"
      filter{ it.replaceAll('%%MAIN_CLASS%%', mainClass.replaceAll( '\\.', '/' ))}
	}
	into("src/main/c")
	rename('java.tpl.c', 'java.c')
}

/* Deletes existing resources and setup files. */
task cleanResources(type: Delete, group: 'build', description: 'Deletes existing resources and setup files.') {

	dependsOn generateJavaC
	mustRunAfter generateJavaC
	
	doLast {
        
        delete 'src/main/c/resources.res'
		"$buildDir" + '/setup/bin'

		mkdir "$buildDir/setup/bin"
		mkdir "$buildDir/setup/lib"
		mkdir "$buildDir/setup/conf"
    }
}

/* Builds the .res files with 'windres'. */
task buildResources(type: Exec, group: 'build', description: 'Builds the .res files.'){
    
	dependsOn cleanResources
	mustRunAfter cleanResources

	workingDir 'src/main/c/'
    environment "PATH", execPath

    commandLine windres, 'resources.rc', '-O', 'coff', '-o', 'resources.res'
}

/* Downloads Java. */
task getJava (group: 'build', description: 'Downloads Java.'){
	doLast {
		ant.get(src: 'https://cdn.azul.com/zulu/bin/zulu15.27.17-ca-jre15.0.0-win_x64.zip', dest: 'build', skipexisting: 'true')
	}
}

/* Extracts Java. */
task extractJava (type: Copy, group: 'build', description: 'Extracts Java.'){
	
	dependsOn getJava
	mustRunAfter getJava
	
	from zipTree("$buildDir/zulu15.27.17-ca-jre15.0.0-win_x64.zip")
    into "$buildDir/java"
}

/* Integrate Java into setup folder. */
task integrateJava (group: 'build', description: 'Integrates Java into setup folder.'){
	
	dependsOn extractJava
	mustRunAfter extractJava
	
	doLast {
		file("$buildDir/java").listFiles().each { File file ->
			file.listFiles().each { File cont ->
	            ant.move file: cont.absolutePath, todir: "${buildDir}/setup/jre"
	        }
        }
	}
}

/* Compiles exe file using gcc. */
task makenativeexec(type: Exec, group: 'build', description: 'Compiles exe file using gcc.'){
	
	dependsOn buildResources
	mustRunAfter buildResources
	
	workingDir 'src/main/c/'
    environment "PATH", execPath
    
	commandLine make,
	'all',
	'JAVAPATH=' + javaHome,
	'TARGETPATH=' + "${buildDir}/setup/bin"
	
	standardOutput = new ByteArrayOutputStream()
	ext.output = {
		return standardOutput.toString()
	}	
}

/* Creates the nsis setup. */
task buildsetup(type: Exec, group: 'build', description: 'Creates the nsis setup.'){
	
	dependsOn makenativeexec, integrateJava
	mustRunAfter makenativeexec
	
	workingDir 'src/main/nsis'
    
    commandLine nsisHome + '\\bin\\makensis.exe',
    '/DPROG_NAME=' + progName,
	'/DVERSION=' + progVers,
	'/DOUTDIR=' + "$buildDir\\setup",
	'setup.nsi'

}